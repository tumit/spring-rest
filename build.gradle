buildscript {
    ext {
        springBootVersion = '2.1.2.RELEASE'
        springfoxSwaggerVersion = '2.9.2'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    }
}

plugins {
    id 'org.unbroken-dome.test-sets' version '2.0.3'
    id 'com.adarshr.test-logger' version '1.6.0'
    id 'jacoco'
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'zyx.tumit'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

testSets {
    integrationTest { dirName = 'test-integration' }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
        html.destination file("${buildDir}/reports/jacoco")
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [
                            '**/*Config.**',
                            '**/*Application.**'
                    ])
        })
    }
    executionData(test, integrationTest)
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "com.h2database:h2"

    implementation "io.springfox:springfox-swagger2:$springfoxSwaggerVersion"
    implementation "io.springfox:springfox-swagger-ui:$springfoxSwaggerVersion"

    implementation "org.apache.commons:commons-lang3:3.8.1"


    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
